allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'

    sourceCompatibility = '1.8'

    tasks.withType(AbstractArchiveTask) {
        preserveFileTimestamps = false
        reproducibleFileOrder = true
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    buildscript {
        repositories {
            jcenter()
            maven {
                url "https://maven-central.storage-download.googleapis.com/maven2/"
            }
            mavenCentral()
        }
    }
}

subprojects {
    repositories {
        flatDir {
            // Note: This path is relative to the sub-project
            dirs '../../../../java/yfiles/v2_15'
        }
        jcenter()
        mavenCentral()
    }

}

// Downloads all projects dependencies from the repository
task resolveDependencies {
  doLast {
    rootProject.allprojects { project ->
      project.buildscript.configurations.forEach { configuration ->
        if (configuration.canBeResolved) {
          configuration.resolve()
        }
      }
      project.configurations.forEach { configuration ->
        if (configuration.canBeResolved) {
          configuration.resolve()
        }
      }
    }
  }
}
